_realname=suscan
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=v0.3.0.r1062.c3b1fd1
pkgrel=1
pkgdesc="Channel scanner based on sigutils library, development version (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/BatchDrake/suscan"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-sigutils-git"
         "${MINGW_PACKAGE_PREFIX}-libsndfile"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "git")
source=("git+${url}.git#branch=develop"
        "git+https://github.com/antoniovazquezblanco/cmake-gitversiondetect.git"
        "git+https://github.com/antoniovazquezblanco/cmake-pcfilegenerator.git"
        "git+https://github.com/antoniovazquezblanco/cmake-relativefilemacro.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.r%s.%s" "$(git describe --tags --abbrev=0)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  git submodule init
  git config submodule.cmake/cmake-gitversiondetect.url "$srcdir/cmake-gitversiondetect"
  git config submodule.cmake/cmake-pcfilegenerator.url "$srcdir/cmake-pcfilegenerator"
  git config submodule.cmake/cmake-relativefilemacro.url "$srcdir/cmake-relativefilemacro"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "${srcdir}/${_realname}"

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -GNinja \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
